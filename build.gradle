plugins {
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  id 'com.diffplug.spotless' version '5.12.4'
  id 'com.github.spotbugs' version '4.6.1'
  id 'java'
  id 'application'
}

//sourceCompatibility = '16'
//targetCompatibility = '16'

mainClassName = 'org.ogolem.general.MainOgolem'
applicationDefaultJvmArgs = ["--add-modules=jdk.incubator.foreign", "--add-modules=jdk.incubator.vector"]

version = 'snapshot'

repositories {
 flatDir { dirs 'libs'}
 mavenCentral()
}

dependencies {
  implementation name: 'atomdroiduff-2.0.0'
  implementation name: 'corrmd'
  implementation name: 'evbqmdff'
  implementation name: 'lionbench'
  implementation name: 'mpi'
  implementation name: 'scaTTM3F'
  implementation name: 'skalevala'
  implementation 'org.apache.commons:commons-math3:3.6.1'
  implementation 'org.ejml:ejml-all:0.40'
  implementation 'com.github.fommil.netlib:native_ref-java:1.1'
  implementation 'org.jgrapht:jgrapht-core:1.3.1'
  implementation 'org.jgrapht:jgrapht-ext:1.3.1'
  implementation 'org.jgrapht:jgrapht-io:1.3.1'
  implementation 'org.scala-lang:scala-library:2.13.5'
  implementation 'org.slf4j:slf4j-api:1.7.30'
  implementation 'org.slf4j:slf4j-simple:1.7.30'
  testImplementation(
    'junit:junit:4.12',
    'org.junit.jupiter:junit-jupiter-api:5.4.2'
  )
  testRuntimeOnly (
    'org.junit.jupiter:junit-jupiter-engine:5.4.2',
    'org.junit.vintage:junit-vintage-engine:5.4.2'
  )
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(16)
  }
}

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }

  test {
    java {
      srcDirs = ['tests']
    }
  }
}

compileJava {
  options.warnings = true
  options.deprecation = true
  options.debug = true
  options.compilerArgs += ["--add-modules=jdk.incubator.foreign"]
  options.compilerArgs += ["--add-modules=jdk.incubator.vector"]
}

// This debug target can be used from within Netbeans. Note that the "Debug Project"
// button does not work! However, you can open this file inside Netbeans (via
// Projects -> Build Scripts -> build.gradle) and then in the Navigator pane
// select and execute the "debug" task from the "Other Tasks" category.
// From there, Netbeans debugging works, i.e., pausing and continuing of execution
// with the regular buttons. Stopping seems to not work (yet)
// Obviously, adjustments to this target may be necessary for the specific debugging
// intended.
task(debug, dependsOn: 'classes', type: JavaExec) {
  main = 'org.ogolem.general.MainOgolem'
  classpath = sourceSets.main.runtimeClasspath
  // adjust these for your debugging needs
  args '--shmem', 'ar38_sweden.ogo', '1'
  workingDir 'examples/clusteropt/ar38/'
  debug true
}

test {
  useJUnitPlatform()
  // branches != production lack implementation of NUMAL algorithms
  exclude 'org/ogolem/locopt/FleminLocOptTest*'
  exclude 'org/ogolem/locopt/PraxisLocOptTest*'
  exclude 'org/ogolem/locopt/RNK1MinLocOptTest*'
  jvmArgs '--add-modules=jdk.incubator.foreign', '--add-modules=jdk.incubator.vector'
}

shadowJar {
  classifier = null
  minimize{
    exclude(dependency('org.ogolem:.*:.*'))
    exclude(dependency('contrib.atomdroiduff:.*:.*'))
  }
}

spotless {
  ratchetFrom '00fb668'
  java {
    googleJavaFormat()
  }
}

spotbugs {
    ignoreFailures = true
    effort = 'more'
    onlyAnalyze = [ 'org.ogolem.*' ]
}

//spotbugsMain {
//  reports {
//    html {
//      enabled = true
//      stylesheet = 'fancy-hist.xsl'
//    }
//  }
//}
